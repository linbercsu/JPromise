def versionNameStr = '1.0.0'

def copyBinFile = { File src,File dest->

    def input = src.newDataInputStream()
    def output = dest.newDataOutputStream()

    output << input

    input.close()
    output.close()
}

def copyOutPut = {File source, File target ->
    if (target.exists()) {
        target.delete()
    }

    target.getParentFile().mkdirs()

    copyBinFile source, target
}

tasks.whenTaskAdded(new Action<Task>() {
    @Override
    void execute(Task task) {
        if (task.name.equals('packageReleaseJar')) {
            task.doLast {
                for (File f : task.outputs.files.files) {
                    copyOutPut f, new File('jars', "promise-${versionNameStr}.jar")
                    break
                }
            }
        }
    }
})

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "${versionNameStr}"
    }
    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.assertj:assertj-core:1.6.1'
}

group = 'net.zentertain.promise'

def siteUrl = 'https://github.com/linbercsu/JPromise'
def gitUrl = 'https://github.com/linbercsu/JPromise.git'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

//    configurations = ['archives'] //When uploading configuration files
    filesSpec {
        from 'jars'
        into "net/zentertain/promise/promise/${versionNameStr}"
    }
    pkg {
        repo = 'maven'
        name = 'promise'
        desc = 'A simple promise library on Android'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['java', 'android', 'promise']
        publicDownloadNumbers = true

        version {
            name = '1.0.0'
            vcsTag = 'v1.0.0'

        }
    }
}







